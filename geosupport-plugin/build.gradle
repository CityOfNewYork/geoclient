plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'com.diffplug.spotless' version '6.22.0'
}

repositories {
    gradlePluginPortal()
}

//def functionalTest = sourceSets.create('functionalTest')
sourceSets {
    functionalTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    functionalTestImplementation.extendsFrom implementation
    functionalTestRuntimeOnly.extendsFrom runtimeOnly
}

def functionalTestTask = tasks.register('functionalTest', Test) {
    group = 'verification'
    description 'Runs functional tests for the GeosupportIntegrationTestPlugin.'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    // Using JUnitPlatform for running tests
    useJUnitPlatform()
    testLogging {
        info {
            events "failed", "skipped", "passed"
            showStandardStreams = true
        }
    }
}

tasks.named("check") {
    dependsOn functionalTestTask
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest
    plugins {
        geosupportPlugin {
            id = 'com.digitalclash.geoclient.gradle.geosupport'
            implementationClass = 'com.digitalclash.geoclient.gradle.GeosupportPlugin'
        }
        geosupportIntegrationTestPlugin {
            id = 'com.digitalclash.geoclient.gradle.geosupport-integration-test'
            implementationClass = 'com.digitalclash.geoclient.gradle.GeosupportIntegrationTestPlugin'
        }
    }
}

dependencies {
    //compileOnly gradleApi()

    //
    // This config is based on the Gradle documentation 'Testing Plugins',
    // section 'Configuring a test framework':
    // https://docs.gradle.org/current/userguide/testing_gradle_plugins.html
    //
    testImplementation platform("org.spockframework:spock-bom:2.2-groovy-3.0")
    testImplementation('org.spockframework:spock-core') {
        exclude group: 'org.codehaus.groovy'
    }
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.14.9'

    //functionalTestImplementation gradleTestKit()
    //
    // This config is based on the Gradle documentation 'Testing Build Logic with TestKit',
    // section 'Example: Automatically injecting the code under test classes into test builds':
    // https://docs.gradle.org/current/userguide/test_kit.html#example_automatically_injecting_the_code_under_test_classes_into_test_builds
    //
    functionalTestImplementation platform("org.spockframework:spock-bom:2.2-groovy-3.0")
    functionalTestImplementation('org.spockframework:spock-core') {
        exclude group: 'org.codehaus.groovy'
    }
    functionalTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

publishing {
    repositories {
        maven {
            name = 'localPluginRepository'
            url = '../../local-plugin-repository'
        }
    }
}

spotless {
    ratchetFrom 'origin/main'
    format 'misc', {
            target '*.gradle', '*.md', '.gitignore'
            endWithNewline()
            indentWithSpaces()
            trimTrailingWhitespace()
    }
    java {
        endWithNewline()
        //eclipse()
        importOrderFile(rootProject.file('src/spotless/eclipse.importorder'))
        indentWithSpaces()
        licenseHeaderFile(rootProject.file('src/spotless/apache-license-2.0.java'))
        toggleOffOn()
        trimTrailingWhitespace()
    }
}

task printSourceSets(){

    doLast{
        sourceSets.each { srcSet ->
            println "["+srcSet.name+"]"
            print "-->Source directories: "+srcSet.allJava.srcDirs+"\n"
            print "-->Output directories: "+srcSet.output.classesDirs.files+"\n"
            print "-->Compile classpath:\n"
            srcSet.compileClasspath.files.each {
                print "  "+it.path+"\n"
            }
            println ""
        }
    }
}
