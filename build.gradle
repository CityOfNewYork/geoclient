allprojects {

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    compileJava.options*.compilerArgs = [
        '-Xlint:serial', '-Xlint:varargs', '-Xlint:cast', '-Xlint:classfile',
        '-Xlint:dep-ann', '-Xlint:divzero', '-Xlint:empty', '-Xlint:finally',
        '-Xlint:overrides', '-Xlint:path', '-Xlint:-processing', '-Xlint:static',
        '-Xlint:try', '-Xlint:fallthrough', '-Xlint:rawtypes', '-Xlint:deprecation',
       '-Xlint:unchecked', '-Xlint:-options', '-Werror']

    compileTestJava.options*.compilerArgs = [
        '-Xlint:serial', '-Xlint:-varargs', '-Xlint:cast', '-Xlint:classfile',
        '-Xlint:dep-ann', '-Xlint:divzero', '-Xlint:empty', '-Xlint:finally',
        '-Xlint:overrides', '-Xlint:path', '-Xlint:-processing', '-Xlint:static',
        '-Xlint:try', '-Xlint:-fallthrough', '-Xlint:-rawtypes', '-Xlint:-deprecation',
        '-Xlint:-unchecked', '-Xlint:-options']

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    ext {
        enableJavadoc = getConfigProperty('nojdoc') ? false : true
        gsIncludePath = new File(projectDir, 'lib/geosupport/headers')
        gsLibraryPath = getGeosupportLibraryDir()
        gsGeofilesPath = getGeofilesDir()
        localLibsDir = new File(rootProject.buildDir, 'libs')
        pomFile = file("${project.buildDir}/libs/generated-pom.xml")
    }

    test {
        useJUnitPlatform()
        // Variables set in gradle/env.gradle
        systemProperty 'java.library.path', gsLibraryPath
        // Ensure Geosupport knows where to find its data files
        environment 'GEOFILES', gsGeofilesPath
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
        testCompileOnly 'junit:junit:4.12'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.1.0'
        testImplementation 'org.assertj:assertj-core:3.11.1'
        //testImplementation('org.mockito:mockito-core:${mockitoVersion}') {
        //    exclude group:'org.hamcrest', module:'hamcrest-core'
        //}
    }

    // Disable Javadoc if requested
    tasks.withType(Javadoc).all { enabled = project.ext.enableJavadoc }

    jar {
        manifest {
            attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
            )
        }

        from("${rootProject.projectDir}/src/dist") {
            include 'LICENSE.txt'
            include 'NOTICE.txt'
            into 'META-INF'
            expand(copyright: new Date().format('yyyy'), version: project.version)
        }
    } // jar

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        repositories {
            maven {
              name = 'localBuild'
              url = "file://${project.buildDir}/repo"
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                customizePom(pom, project)
                if (signing.required) {
                    signPom(delegate, pom, project)
                }
            }
        }
    }

} // allprojects

// TODO Put this in buildScript plugin
File getGeosupportLibraryDir() {
    def gsLibraryPath = getConfigProperty('gsLibraryPath')
    if(!gsLibraryPath) {
        return new File(getGeosupportHome(), 'lib')
    }
    return new File(gsLibraryPath)
}

File getGeofilesDir() {
    def gsGeofilesPath = getConfigProperty('gsGeofilesPath')
    if(!gsGeofilesPath) {
        return new File(getGeosupportHome(), 'fls/')
    }
    return new File(gsGeofilesPath)
}

String getGeosupportHome() {
    return getConfigProperty('gosupportHome') ?: System.getenv('GEOSUPPORT_HOME')
}

// https://github.com/bmuschko/gradle-docker-plugin/blob/v3.6.2/gradle/test-setup.gradle
String getConfigProperty(String key) {
    project.properties.getOrDefault(key, System.getProperty(key, System.getenv(key)))
}

def customizePom(pom, project) {
    pom.withXml {
        def root = asNode()
        root.appendNode('name', project.name)
        root.appendNode('description', project.description)
        root.appendNode('url', 'https://github.com/CityOfNewYork/geoclient')
        root.appendNode('inceptionYear', '2013')

        def scm = root.appendNode('scm')
        scm.appendNode('url', 'https://github.com/CityOfNewYork/geoclient')
        scm.appendNode('connection', 'scm:https://git@github.com/CityOfNewYork/geoclient.git')
        scm.appendNode('developerConnection', 'scm:git://github.com/CityOfNewYork/geoclient.git')

        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode('name', 'The Apache Software License, Version 2.0')
        license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
        license.appendNode('distribution', 'repo')

        def organization = root.appendNode('organization')
        organization.appendNode('name', 'City of New York / DoITT / Citywide GIS')
        organization.appendNode('url', 'https://maps.nyc.gov')

        def developers = root.appendNode('developers')
        def mlipper = developers.appendNode('developer')
        mlipper.appendNode('id', 'mlipper')
        mlipper.appendNode('name', 'Matthew Lipper')
        mlipper.appendNode('email', 'mlipper@doitt.nyc.gov')

        def issueManagement = root.appendNode('issueManagement')
        issueManagement.appendNode('system', 'GitHub')
        issueManagement.appendNode('url', 'https://github.com/CityOfNewYork/geoclient/issues')
    }
}

def signPom(closure, pom, project) {
    pom.withXml {
        writeTo(project.ext.pomFile)
        def pomAscFile = signing.sign(project.ext.pomFile).signatureFiles[0]
        closure.artifact(pomAscFile) {
            classifier = null
            extension = 'pom.asc'
        }
        project.ext.pomFile.delete()
    }
}
