plugins {
    id "gov.nyc.doitt.gis.geoclient.gradle.integration-test" apply false
    id "io.spring.dependency-management" version "1.0.8.RELEASE" apply false
    id "org.springframework.boot" version "2.2.1.RELEASE" apply false
    id "com.github.ben-manes.versions" version "0.27.0"
}

ext {
    moduleProjects = subprojects.findAll { it.name.startsWith("geoclient-") }
    jarPublishProjects = moduleProjects - project(":geoclient-service")
    springBootProject = project(":geoclient-service")
}

configure(allprojects) { project ->
    task("configs") {
        doLast {
            println ""
            println "${project.name}"
            println "---------------"
            println "moduleProjects: ${moduleProjects}"
            println "jarPublishProjects: ${jarPublishProjects}"
            println "springBootProject: ${springBootProject}"
            //project.configurations.each {
            //    println "${it.name}"
            //    if(it.artifacts) {
            //        println "   artifacts: ${it.artifacts.getFiles().asPath}"
            //    }
            //    if(it.dependencies) {
            //        println "   dependencies: ${it.dependencies.collect { d -> d.name + ":" + (d.version ?: "<unspecified>") }}"
            //    }
            //}
        }
    }
}

configure(moduleProjects){ project ->

    apply plugin: "java"
    apply plugin: "maven-publish"
    apply from: "$rootDir/gradle/publications.gradle"

    // Java dependency configuration
    repositories {
        maven { url "https://nexus.geomatys.com/repository/geotoolkit/" }
        mavenCentral()
        maven { url "https://repo.boundlessgeo.com/main/" }
    }

    ext{

        //checkstyleVersion         = '8.11'
        //commonsLang3Version       = '3.9'
        commonsTextVersion        = '1.8'
        diffutilsVersion          = '1.3.0'
        // Sync version with geoclient-service/src/main/resources/META-INF/spring-devtools.properties
        dozerVersion              = '6.5.0'
        geotoolsVersion           = '19.0'
        httpclient                = '4.5.10'
        thymeleafVersion          = '3.0.11.RELEASE'
        xstreamVersion            = '1.4.11.1'

        licenseFilesSpec = copySpec {
            from("${rootDir}/src/dist") {
                include "license.txt"
                include "notice.txt"
                into "META-INF"
                expand(copyright: new Date().format("yyyy"), version: project.version)
            }
        }
    }

    dependencies {
        implementation enforcedPlatform("org.springframework.boot:spring-boot-dependencies:2.2.1.RELEASE") // Must be in sync with geoclient-service/build.gradle
        implementation "org.springframework.boot:spring-boot-starter-logging"
        testImplementation("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "org.junit", module: "junit"
        }
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    // Java build configuration
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        withJavadocJar()
        withSourcesJar()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    normalization {
        runtimeClasspath {
            ignore "META-INF/MANIFEST.MF"
        }
    }

    // Test Java configuration
    apply plugin: "gov.nyc.doitt.gis.geoclient.gradle.integration-test"

    integrationTest {
        useJUnitPlatform()
        testLogging {
            displayGranularity = 3
            events "failed", "skipped", "passed"
            exceptionFormat "full"
            showStandardStreams = false
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            displayGranularity = 3
            events "failed", "skipped", "passed"
            exceptionFormat "full"
            showStandardStreams = false
        }
    }

    // Javadoc configuration
    ext {
        javadocLinks = [
            "https://docs.oracle.com/javase/8/docs/api/",
            "https://fasterxml.github.io/jackson-core/javadoc/2.10/",
            "https://fasterxml.github.io/jackson-databind/javadoc/2.10/",
            "https://fasterxml.github.io/jackson-dataformat-xml/javadoc/2.10/",
            "https://junit.org/junit5/docs/5.5.2/api/"
        ] as String[]
    }

    javadoc {
        description = "Generates project-level javadoc for use in -javadoc jar"

        options.encoding = "UTF-8"
        options.memberLevel = JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = project.name
        options.use = true
        options.links(project.ext.javadocLinks)
        options.addStringOption("Xdoclint:none", "-quiet")

        // Suppress warnings due to cross-module @see and @link references.
        // Note that global 'api' task does display all warnings.
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO  // suppress "## warnings" message
    }

    // Shared jar manifest configuration
    ext {
        sharedManifest = jar.manifest {
           attributes(
               "Implementation-Title": project.name,
               "Implementation-Version": project.version,
               "Automatic-Module-Name": project.name.replace('-', '.'),  // for Jigsaw
               "Built-By": "${System.getProperty("user.name")}",
               "Created-By": "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})",
               "Built-Gradle": gradle.gradleVersion)
        }
    }
}

//
// Jar packaging & publishing of modules except for :geoclient-service:bootJar 
// 
configure(jarPublishProjects) { project ->
    
    jar {
        from components.java
        manifest = project.manifest {
            from sharedManifest
        }
        with(licenseFilesSpec)
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

//
// // Jar packaging & publishing of geoclient-service module 
// 
// configure(springBootProject) { project ->
// }
