
sourceSets {
  main {
    java {
      srcDirs = ['src/main/java','src/generated/java']
    }
    resources {
      srcDirs = ['src/main/resources']
    }
  }
}

dependencies {
    compile "org.jogamp.gluegen:gluegen:$gluegenVersion"
}

processResources {
  dependsOn ':geoclient-native:assemble'

  ext.libName = "geoclientjni"

  if(project.isWindows) {
    ext.libFilePrefix = ""
    ext.libFileType = "dll"
    ext.platformName = "windows_x64"
  } else {
    ext.libFilePrefix = "lib"
    ext.libFileType = "so"
    ext.platformName = "linux_x64"
  }
  ext.libFileName = "${libFilePrefix}${libName}.${libFileType}"
  ext.libFile = new File("${project(':geoclient-native').buildDir}/libs/geoclientJni/shared/${platformName}/${libFileName}")
  inputs.file(libFile)
  doLast {
    logger.lifecycle("Copying ${libFile.absolutePath} to ${buildDir}/resources/main/${libFilePrefix}${libName}-${platformName}.${libFileType}")
    copy {
      //from project.localLibsDir
      from libFile
      into "${buildDir}/resources/main"
      rename libFileName, "${libFilePrefix}${libName}-${platformName}.${libFileType}"
    }
  }
}

artifacts {
  archives jar
}
